apply plugin: 'com.android.application'
apply plugin: 'com.google.gms.google-services'
apply plugin: 'com.google.firebase.crashlytics'
apply plugin: 'kotlin-android'
apply plugin: 'com.diffplug.spotless'

apply from: '../jacoco.gradle'
apply from: '../ktlint.gradle'
apply from: '../detekt.gradle'
apply from: '../checkstyle.gradle'
apply from: '../sonarcube.gradle'

def localPropertiesFile = rootProject.file("local.properties")
def localProperties = new Properties()

if (!localPropertiesFile.exists()) {
    localProperties.setProperty("RELEASE_STORE_PASSWORD", "")
    localProperties.setProperty("RELEASE_KEY_PASSWORD", "")
    localProperties.setProperty("RELEASE_KEY_ALIAS", "")
    localProperties.setProperty("RELEASE_STORE_FILE", "keystore.jks")
    Writer writer = new FileWriter(localPropertiesFile, false)
    localProperties.store(writer, "empty, as creating the file is done manually via gpg")
    writer.close()

    file(project(':app').projectDir.path + "/keystore.jks").text = ""
}

localProperties.load(new FileInputStream(localPropertiesFile))

android {
    compileSdk 33

    signingConfigs {
        googleplay {
            keyAlias localProperties['RELEASE_KEY_ALIAS']
            keyPassword localProperties['RELEASE_KEY_PASSWORD']
            storeFile file(localProperties['RELEASE_STORE_FILE'])
            storePassword localProperties['RELEASE_STORE_PASSWORD']
        }
    }

    defaultConfig {
        minSdkVersion 24
        targetSdkVersion 33

        applicationId "com.github.jameshnsears.quoteunquote"

        // changelog version | min sdk | target sdk
        versionCode 1612433
        // semantic versioning
        versionName "4.33.0"

        vectorDrawables.useSupportLibrary = true

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        testInstrumentationRunnerArguments clearPackageData: 'true'

        javaCompileOptions {
            annotationProcessorOptions {
                arguments += ["room.schemaLocation":
                                      "$projectDir/schemas".toString()]
            }
        }
    }

    packagingOptions {
        resources {
            excludes += [
                    '**/module-info.class',
                    'LICENSE',
                    'README.md',
                    'META-INF/LICENSE.md',
                    'META-INF/LICENSE-notice.md'
            ]
        }
    }

    buildTypes {
        def gitHash = { ->
            def stdout = new ByteArrayOutputStream()
            exec {
                commandLine 'git', 'rev-parse', '--short=8', 'HEAD'
                standardOutput = stdout
            }
            return stdout.toString().trim()
        }

        release {
            // false == only occurs in migration to Android Studio Flamingo | 2022.2.1
            minifyEnabled false
            shrinkResources false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'

            buildConfigField("String", "GIT_HASH", "\"$gitHash\"")
            buildConfigField("String", "DATABASE_QUOTATIONS", "\"quotations.db.prod\"")

            ndk {
                debugSymbolLevel 'FULL'
            }
        }

        debug {
            testCoverageEnabled true
            buildConfigField("String", "GIT_HASH", "\"$gitHash\"")
            buildConfigField("String", "DATABASE_QUOTATIONS", "\"quotations.db.prod\"")
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_11
        targetCompatibility JavaVersion.VERSION_11
    }
    kotlinOptions {
        jvmTarget = '11'
    }

    flavorDimensions 'Version'
    productFlavors {
        'googleplay' {
            dimension 'Version'
            versionNameSuffix "-googleplay"
            signingConfig signingConfigs.googleplay
        }
        'fdroid' {
            dimension 'Version'
            versionNameSuffix "-fdroid"
            isDefault true
        }
        'espresso' {
            dimension 'Version'
            versionNameSuffix "-espresso"
        }
        'uiautomator' {
            dimension 'Version'
            versionNameSuffix "-uiautomator"
        }
    }

    sourceSets {
        androidTest {
            assets.srcDirs += files("$projectDir/schemas".toString())
            assets.srcDirs += 'src/androidTest/assets/'
        }
        fdroid {
            assets.srcDirs = ['src/main/assets']
            java.srcDirs = ['src/main/java', 'src/fdroid/java']
        }
        googleplay {
            assets.srcDirs = ['src/main/assets']
            java.srcDirs = ['src/main/java']
        }
        espresso {
            assets.srcDirs = ['src/main/assets']
            java.srcDirs = ['src/main/java']
        }
        uiautomator {
            assets.srcDirs = ['src/main/assets']
            java.srcDirs = ['src/main/java']
        }
    }

    testOptions {
        // will make tests run very slowly on the emulator/device + affects coverage #
        // execution 'ANDROIDX_TEST_ORCHESTRATOR'

        animationsDisabled true
        unitTests {
            includeAndroidResources = true
            returnDefaultValues = true
            all {
                maxHeapSize = "1024m"
                jacoco {
                    includeNoLocationClasses = true
                    excludes = ['jdk.internal.*']
                }
            }
        }
    }

    buildFeatures {
        viewBinding = true
    }

    lint {
        abortOnError false
        baseline file("$rootDir/config/lint/baseline.xml")
        checkAllWarnings true
        htmlReport true
        warningsAsErrors true
        xmlReport true
    }
    namespace 'com.github.jameshnsears.quoteunquote'
}

dependencies {
    androidTestImplementation 'androidx.arch.core:core-testing:2.2.0'
    androidTestImplementation 'androidx.room:room-testing:2.5.1'
    androidTestImplementation 'androidx.test:core:1.5.0'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.5.1'
    androidTestImplementation 'androidx.test.ext:junit:1.1.5'
    androidTestImplementation 'androidx.test.ext:junit-ktx:1.1.5'
    androidTestImplementation 'androidx.test:rules:1.5.0'
    androidTestImplementation 'androidx.test:runner:1.5.2'
    androidTestImplementation 'androidx.test.uiautomator:uiautomator:2.2.0'
    androidTestImplementation 'io.mockk:mockk-android:1.13.5'

    annotationProcessor 'androidx.room:room-compiler:2.5.1'

    debugImplementation 'androidx.fragment:fragment-testing:1.6.0'
    debugImplementation 'androidx.test:core-ktx:1.5.0'
    debugImplementation 'com.squareup.leakcanary:leakcanary-android:2.11'

    googleplayImplementation 'com.google.firebase:firebase-analytics:21.3.0'
    googleplayImplementation 'com.google.firebase:firebase-crashlytics:18.3.7'

    implementation 'androidx.activity:activity-ktx:1.7.2'
    implementation 'androidx.appcompat:appcompat:1.6.1'
    implementation 'androidx.cardview:cardview:1.0.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.1.4'
    implementation 'androidx.core:core-ktx:1.10.1'
    implementation 'androidx.databinding:databinding-runtime:8.0.2'
    implementation 'androidx.multidex:multidex:2.0.1'
    implementation 'androidx.room:room-guava:2.5.1'
    implementation 'androidx.room:room-runtime:2.5.1'
    implementation 'androidx.room:room-rxjava2:2.5.1'
    implementation 'com.github.skydoves:colorpickerpreference:2.0.6'
    implementation 'com.google.android.material:material:1.9.0'
    implementation 'com.google.code.gson:gson:2.10.1'
    implementation 'com.google.guava:guava:32.0.1-jre'
    implementation 'com.jakewharton.rxbinding2:rxbinding:2.2.0'
    implementation 'com.jakewharton.timber:timber:5.0.1'
    implementation 'com.squareup.okhttp3:okhttp:4.11.0'
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    implementation 'io.reactivex.rxjava2:rxandroid:2.1.1'
    implementation 'io.reactivex.rxjava2:rxjava:2.2.21'
    implementation 'net.pwall.json:json-kotlin-schema:0.39'
    implementation 'org.apache.commons:commons-csv:1.10.0'
    implementation 'org.jsoup:jsoup:1.16.1'
    implementation platform('com.google.firebase:firebase-bom:32.1.0')
    implementation project(path: ':cloudLib')
    implementation project(path: ':utilsLib')

    testImplementation 'androidx.arch.core:core-testing:2.2.0'
    testImplementation 'androidx.room:room-testing:2.5.1'
    testImplementation 'androidx.test:core-ktx:1.5.0'
    testImplementation 'androidx.test.ext:junit:1.1.5'
    testImplementation 'androidx.test:rules:1.5.0'
    testImplementation 'ch.qos.logback:logback-classic:1.4.7'
    testImplementation 'com.google.guava:guava:32.0.0-jre'
    testImplementation 'io.mockk:mockk:1.13.5'
    testImplementation 'junit:junit:4.13.2'
    testImplementation 'org.jetbrains.kotlinx:kotlinx-coroutines-test:1.7.1'
    testImplementation 'org.robolectric:robolectric:4.10.3'
}

repositories {
    mavenCentral()
}
