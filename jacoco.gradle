apply plugin: 'jacoco'

jacoco {
    toolVersion = "0.8.8"
}

project.afterEvaluate {
    def buildTypes = android.buildTypes.collect { type -> type.name }
    def productFlavors = android.productFlavors.collect { flavor -> flavor.name }
    def buildDirPath = layout.buildDirectory.get().asFile

    if (!productFlavors) productFlavors.add('')

    productFlavors.each { productFlavorName ->
        buildTypes.each { buildTypeName ->
            def sourceName
            if (!productFlavorName) {
                sourceName = "${buildTypeName}"
            } else {
                sourceName = "${productFlavorName}${buildTypeName.capitalize()}"
            }
            def testTaskName = "test${sourceName.capitalize()}UnitTest"

            def excludes = [
                    '**/BuildConfig.*',
                    '**/*_Impl*.*',
                    '**/*Binding.*',
                    '**/*Double.*',
                    '**/*Binding.*'
            ]

            def classKotlin = fileTree(dir: "$buildDirPath/tmp/kotlin-classes/${sourceName}UnitTest", excludes: excludes)
            def classJava = fileTree(dir: "$buildDirPath/intermediates/javac/${sourceName}/classes", excludes: excludes)

            def kotlinSrc = "$project.projectDir/src/main/java"
            def javaSrc = "$project.projectDir/src/main/java"
            def flavourSrc = "$project.projectDir/src/$productFlavorName/java"

            task "test${sourceName.capitalize()}CombinedCoverage"(type: JacocoReport, dependsOn: ["$testTaskName", "connected${sourceName.capitalize()}AndroidTest"]) {
                group = "coverage.combined"
                description = "Generate Jacoco /test + combined (if avail.) /andoidTest coverage report from ${sourceName.capitalize()}"

                classDirectories.setFrom(files([classKotlin, classJava]))

                sourceDirectories.setFrom(files([javaSrc, kotlinSrc, flavourSrc]))

                executionData.from = fileTree(dir: "${buildDirPath}", includes: [
                        "outputs/unit_test_code_coverage/${sourceName}UnitTest/${testTaskName}.exec",
                        "outputs/code_coverage/$productFlavorName${buildTypeName.capitalize()}AndroidTest/connected/**/*.ec"
                ])

                reports {
                    xml.required = true
                    xml.outputLocation.set(file("${buildDirPath}/reports/${sourceName.capitalize()}.xml"))
                    html.required = true
                    html.outputLocation.set(file("$buildDirPath/reports/jacoco"))
                }
            }

            ///////////////////////////////////////

            task "test${sourceName.capitalize()}UnitTestCoverage"(type: JacocoReport, dependsOn: ["$testTaskName"]) {
                group = "coverage.unittest"
                description = "Generate Jacoco /test coverage report from ${sourceName.capitalize()}"

                classDirectories.setFrom(files([classKotlin, classJava]))

                sourceDirectories.setFrom(files([javaSrc, kotlinSrc, flavourSrc]))

                println "$sourceName"

                executionData.from = fileTree(dir: "${project.buildDir}", includes: [
                        "outputs/unit_test_code_coverage/${sourceName}UnitTest/${testTaskName}.exec"
                ])

                reports {
                    xml.required = true
                    xml.outputLocation.set(file("${project.buildDir}/reports/${sourceName.capitalize()}.xml"))
                    html.required = true
                    html.outputLocation.set(file("$project.buildDir/reports/jacoco"))
                }
            }

            ///////////////////////////////////////

            task "connected${sourceName.capitalize()}AndroidTestCoverage"(type: JacocoReport, dependsOn: ["connected${sourceName.capitalize()}AndroidTest"]) {
                group = "coverage.connected"
                description = "Generate Jacoco /test coverage report from ${sourceName.capitalize()}"

                classDirectories.setFrom(files([classKotlin, classJava]))

                sourceDirectories.setFrom(files([javaSrc, kotlinSrc, flavourSrc]))

                println "$sourceName"

                executionData.from = fileTree(dir: "${project.buildDir}", includes: [
                        "outputs/code_coverage/$productFlavorName${buildTypeName.capitalize()}AndroidTest/connected/**/*.ec"
                ])

                reports {
                    xml.required = true
                    xml.outputLocation.set(file("${project.buildDir}/reports/${sourceName.capitalize()}.xml"))
                    html.required = true
                    html.outputLocation.set(file("$project.buildDir/reports/jacoco"))
                }
            }

        }
    }
}
